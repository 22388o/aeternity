main contract StakingValidator =
  record state =
    { main_staking_ct : address,
      name : string,
      description : string,
      avatar_url : string,
      delegates : map(address, int),
      shares : int }

  entrypoint init(main_staking_ct : address) =
    { main_staking_ct = main_staking_ct,
      name = "",
      description = "",
      avatar_url = "",
      delegates = {},
      shares = 0 }

  payable stateful entrypoint stake(staker : address) =
    assert_caller()
    require(Call.value > 0, "Stake must be > 0")
    let new_shares = calculate_shares(Call.value)
    add_shares(staker, new_shares)
    put(state{shares @ s = s + new_shares})

  payable entrypoint profit() =
    assert_caller()
    ()

  stateful entrypoint unstake(staker : address, withdraw_shares : int) : int =
    assert_caller()
    let staker_shares = state.delegates[staker = 0]
    require(staker_shares >= withdraw_shares, "Not enough shares")
    let payout = (withdraw_shares * Contract.balance) / state.shares
    Chain.spend(staker, payout)
    let remaining_shares = staker_shares - withdraw_shares
    switch(remaining_shares == 0)
      true =>
        put(state{ shares @ s = s - withdraw_shares,
                  delegates @ ds = Map.delete(staker, ds) })
      false =>
        put(state{ shares @ s = s - withdraw_shares,
                  delegates[staker] @ balance = remaining_shares })
    payout

  entrypoint get_state() = state

  stateful entrypoint set_name(name : string) =
    assert_caller()
    put(state{name = name})

  stateful entrypoint set_description(description : string) =
    assert_caller()
    put(state{description = description})

  stateful entrypoint set_avatar_url(avatar_url : string) =
    assert_caller()
    put(state{avatar_url = avatar_url})

  stateful function add_shares(staker : address, shares : int) =
    put(state{ delegates[staker = 0] @ dshares = dshares + shares })

  function calculate_shares(stake_size : int) =
    if (state.shares == 0)
      stake_size
    else
      (stake_size * state.shares) / (Contract.balance - stake_size)

  function assert_caller() =
    require(Call.caller == state.main_staking_ct, "Only allowed through MainStaking")
